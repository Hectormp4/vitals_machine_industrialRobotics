#include <DIYablesWebApps.h>
// WiFi credentials - UPDATE THESE WITH YOUR NETWORK
const char WIFI_SSID[] = "NETWORK NAME";
const char WIFI_PASSWORD[] = "NETWORK PASSWORD";
// Create web app instances
UnoR4ServerFactory serverFactory;
DIYablesWebAppServer server(serverFactory, 80, 81);  // HTTP port 80, WebSocket port 81
DIYablesHomePage homePage;
DIYablesWebTemperaturePage temperaturePage(-10.0, 50.0, "째C");  // Min: -10째C, Max: 50째C
// Temperature simulation variables
float currentTemp = 35.0;  // Starting temperature
unsigned long lastUpdate = 0;
unsigned long perviousTime = 0;
unsigned long holdStartTime = 0;
const long interval = 10000;
const long holdDuration = 8000;  // 8 seconds hold time
bool tempIncreasing = true;
bool holdingAtMax = false;
void setup() {
  Serial.begin(9600);
  Serial.println("Starting Web Temperature Server...");
  // Add web apps to server
  server.addApp(&homePage);
  server.addApp(&temperaturePage);
  // Set 404 Not Found page (optional - for better user experience)
  server.setNotFoundPage(DIYablesNotFoundPage());
  // Set up temperature callback for value requests
  temperaturePage.onTemperatureValueRequested(onTemperatureValueRequested);
  // Start the server
  server.begin(WIFI_SSID, WIFI_PASSWORD);
}
void loop() {
  // Handle web server and WebSocket communications
  server.loop();
  // Simulate temperature readings
  simulateTemperature();
  // Send temperature update every 2 seconds
  if (millis() - lastUpdate >= 2000) {
    temperaturePage.sendTemperature(currentTemp);
    // Print temperature to Serial Monitor
    Serial.println("Temperature: " + String(currentTemp, 1) + "째C");
    lastUpdate = millis();
  }
}
void simulateTemperature() {
  unsigned long currentTime = millis();
  
  // If holding at max temperature, wait for hold duration
  if (holdingAtMax) {
    if (currentTime - holdStartTime >= holdDuration) {
      holdingAtMax = false;
      tempIncreasing = false;  // Start decreasing
      perviousTime = currentTime;  // Reset the interval timer
    }
    return;  // Don't change temperature while holding
  }
  
  if (currentTime - perviousTime >= interval){
    
    float increment = random(2500, 5500) / 1000.0 ;
  
    if (tempIncreasing) {
      currentTemp += increment;
      if (currentTemp >= 50.0){
        currentTemp = 50.0;
        holdingAtMax = true;  // Start holding at max
        holdStartTime = currentTime;  // Record when we reached max
      }
    } else {
      currentTemp -= increment;
      if (currentTemp <= -10.0){
        currentTemp = -10.0;
        tempIncreasing = true;  // Start increasing again
      }
    }
    
    perviousTime = currentTime;
  }
}
/**
 * Callback function called when web interface requests temperature value
 * Send current temperature value to web interface
 */
void onTemperatureValueRequested() {
  Serial.println("Temperature value requested from web interface");
  // Send current temperature value (config is automatically sent by the library)
  temperaturePage.sendTemperature(currentTemp);
}
